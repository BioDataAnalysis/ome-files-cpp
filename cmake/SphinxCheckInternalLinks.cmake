message(STATUS "Checking internal links")

set(failed OFF)

file(STRINGS "${OUTPUT}" output)
file(REMOVE "${OUTPUT}.new")
foreach(line IN LISTS output)
  string(REGEX MATCH "^.*: \\[.*\\] .*" line_match "${line}")
  string(REGEX MATCH "^.*: \\[.*\\] .*#.*" anchor_match "${line}")
  if(line_match)
    string(REGEX REPLACE "^(.*): \\[(.*)\\] (.*)" "\\1" filename "${line}")
    string(REGEX REPLACE "^(.*): \\[(.*)\\] (.*)" "\\2" type "${line}")
    if(anchor_match)
      string(REGEX REPLACE "^(.*): \\[(.*)\\] (.*)#(.*)" "\\3" link "${line}")
      string(REGEX REPLACE "^(.*): \\[(.*)\\] (.*)#(.*)" "\\4" link "${anchor}")
    else()
      string(REGEX REPLACE "^(.*): \\[(.*)\\] (.*)" "\\3" link "${line}")
      unset(anchor)
    endif()

    if(type STREQUAL "local")
      file(RELATIVE_PATH relpath "/" "/${SPHINX_INSTALL_PATH}/${link}")

      unset(linkfile)
      if(EXISTS "${EXTERNAL_REFERENCE}/${relpath}")
        set(linkfile "${EXTERNAL_REFERENCE}/${relpath}")
      endif()
      if(EXISTS "${INTERNAL_REFERENCE}/${link}")
        set(linkfile "${INTERNAL_REFERENCE}/${link}")
      endif()
      if(DOXYGEN_REFERENCE AND DOXYGEN_INSTALL_PATH)
        string(REGEX REPLACE "^${DOXYGEN_INSTALL_PATH}/" "" doxygen_path "${relpath}")
        if(EXISTS "${DOXYGEN_REFERENCE}/${doxygen_path}")
          set(linkfile "${DOXYGEN_REFERENCE}/${doxygen_path}")
        endif()
      endif()
      if(linkfile)
        if(anchor)
          file(STRINGS "${linkfile}" anchor_matches REGEX "<a +class=\"anchor\" +id=\"${anchor}\"")
          if(anchor_matches)
            file(APPEND "${OUTPUT}.new" "${filename}: [${type}] ${link}#${anchor}\n")
          else()
            set(failed ON)
            file(APPEND "${OUTPUT}.new" "${filename}: [broken] ${link}#${anchor}\n")
            message(STATUS "Broken link: ${filename}: ${link}#${anchor}")
          endif()
        else()
          file(APPEND "${OUTPUT}.new" "${filename}: [${type}] ${link}\n")
        endif()
      else()
        set(failed ON)
        file(APPEND "${OUTPUT}.new" "${filename}: [broken] ${link}\n")
        message(STATUS "Broken link: ${filename}: ${link}")
      endif()
    else()
      if(type STREQUAL "broken")
        set(failed ON)
        message(STATUS "Broken link: ${filename}: ${link}")
      endif()
      file(APPEND "${OUTPUT}.new" "${filename}: [${type}] ${link}\n")
    endif()
  else()
    file(APPEND "${OUTPUT}.new" "${line}\n")
  endif()
endforeach()

file(RENAME "${OUTPUT}.new" "${OUTPUT}")

if(failed)
  message(FATAL_ERROR "Broken links detected")
endif()
